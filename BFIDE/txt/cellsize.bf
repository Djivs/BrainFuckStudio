
[The programs listed below are written by me (rdebath) and so are officially
relased as public domain or CC0. The other BF programs are either written
by third parties or mechanical translations of such and so not covered by
this note.  However, unless noted within the original file, you have a good
faith expectation that nobody cares, so they are public domain by default.

    Cellsize.b Cellsize2.b Cellsize3.b Cellsize4.b Cellsize5.b Counter.b
    EasyOpt.b Endtest.b Hello.b Hello2.b Precalc.b PrecalcBreak.b Prttab.b
    Skiploop.b Tribit.b al-count-1.b al-count-2.b cells100k.b cells30k.b]


// Calculate (16*16*16*16)^128 AKA 2^2048
++[>++++++++<-]>[>++++++++<-]>[<<
+>>[-<<
    [->++++++++++++++++<]>[-<++++++++++++++++>]<
    [->++++++++++++++++<]>[-<++++++++++++++++>]<
>>]]<+<
// If that's not zero we have a bignum; probably
[
    // Calculate 2^112 this is too large for any floating point type
    // to treat as an integer
    [-]>[-]>[-]+++++++<<
    +>>[-<<
	[->++++++++++++++++<]>[-<++++++++++++++++>]<
	[->++++++++++++++++<]>[-<++++++++++++++++>]<
    >>]<<
    // If X and X minus one are the same we have a float type
    [->+>+<<] >>- [-<->] +<
    [[-]>
    // Unknown or huge cell size
    >>>>>[-]<[-]<[-]<[-]<[-]<[-]>++++++[<++++>-]<[>+++++>++++>+>++>+
    ++<<<<<-]>>>>>.<<<<---.>+++++++.--.>++++++++.<<------.+++.>>.<<-
    ---.+.-.>>>---.<<---.+++++++.<.>--------.<++++.+++++++.>>.<++.++
    .<-------------..+++++++.>>.<+.<----.++++++.-------.>--.>>+.>[-]
    <[-]<[-]<[-]<[-]<
    <]
    >[
    // Floating point cells found
    >>>>[-]<[-]<[-]<[-]<[-]>++++[<++++>-]<[>++++>+++++++>++++++>++<<
    <<-]>++++++.>----.+++.>+.<+++++.>++++++++.+++++.-------.>.<<----
    .-.>++.<-.++++++.>>.<------.++.+++++++..<-.>>.<------.<----.++++
    ++.-------.>--.>++++++++++++++.[-]<[-]<[-]<[-]<
    ]<
    // Check for a compile problem
    >[-]+++++[<++++++>-]<++ >+<[->[->++<]>[-<+>]<<]
    #
    >>+<[[-]>-<]>[
	>[-]<[-]>++++++++[<++++>-]<.>++++++[<++++++>-]<+.+++++++++++
	++..---.+++.[-]
    ]<<
#
<[-]]
>[>
    // The cell is a binary type less than 2^2048; find exactly how
    // many bits it is
    >+[ [->++<]>[-<+>]<<+> ]<
    // and print it
    >>++++[<++++>-]<+[>++++++>+++++++>++>+++++<<<<-]>>>>-.<<<++.+.>-
    ---.>--.<<.+++++.>+.<---------.>--.--.++.<.>++.<.>--.>.<<+++.---
    ----.>+.>.>[-]<[-]<[-]<[-]<<
    >[-]>[-]>[-]>[-]>[-]>[-]+>[-]+>[-]+>[-]+<<<<<<<<<[->>+>+<<<]>>>[
    -<<<+>>>]<[>[-]>[-]<<[>+>+<<-]>>[<<+>>-]+++++++++<[>>>[-]+<<[>+>
    [-]<<-]>[<+>-]>[<<++++++++++>>-]<<-<-]+++++++++>[<->-]<[>+<-]<[>
    +<-]<[>+<-]>>>[<<<+>>>-]>>>[-]<<<+++++++++<[>>>[-]+<<[>+>[-]<<-]
    >[<+>-]>[<<++++++++++>>>+<-]<<-<-]>>>>[<<<<+>>>>-]<<<[-]<<+>]<[>
    [-]<[>+<-]+++++++[<+++++++>-]<-.[-]>>[<<+>>-]<<-]<
    >>>[-]<[-]<[-]<[-]>++++[<++++>-]<[>++++++>+++++++>++<<<-]>++.+++
    ++++.>++++.>.<<------.++.+++++++..>-.>++++++++++++++.[-]<[-]<[-]
    <
<[-]]<
[-]++++++++++.[-]
